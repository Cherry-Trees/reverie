start: 
    | use_stmts stmts

use_stmt: 
    | "use" NAME { "." NAME } ";"

use_stmts: 
    | { use_stmt }

stmt:
    | class_decl
    | fn_decl
    | if_stmt
    | for_stmt
    | while_stmt
    | expr ";"

stmts: 
    | { stmt }

fn_stmt:
    | class_decl
    | fn_decl
    | fn_if_stmt
    | fn_for_stmt
    | fn_while_stmt
    | return_stmt
    | expr ";"

fn_stmts:
    | { fn_stmt }

class_decl:
    | "class" NAME "(" [ name_sequence ] ")" [ "extends" NAME ] class_block

class_block:
    | "{" fn_decls "}"

fn_decl: 
    | "fn" NAME "(" [ name_sequence ] ")" fn_block

fn_decls:
    | { fn_decl }

block:
    | "{" stmts "}"

fn_block:
    | "{" fn_stmts "}"

if_stmt:
    | "if" expr block [ else_stmt ]

else_stmt:
    | "else" block

for_stmt:
    | "for" NAME "in" expr block -- expr has to evaluate to a ListObj

while_stmt:
    | "while" expr block

return_stmt:
    | "return" [ expr ]

fn_if_stmt:
    | "if" expr fn_block [ fn_else_stmt ]

fn_else_stmt
    | "else" fn_block

fn_for_stmt:
    | "for" NAME "in" expr fn_block

fn_while_stmt:
    | "while" expr fn_block

name_sequence:
    | NAME { "," NAME }

expr_sequence:
    | expr { "," expr }

expr:
    | var "=" expr
    | expr_5

expr_5:           
    | expr_4 { "or" expr_5 }

expr_4:           
    | expr_3 { "and" expr_4 }

expr_3: 
    | ["not"] expr_2 { ("=="|"!=") expr_3 }

expr_2:           
    | expr_1 { ("<"|"<="|">"|">=") expr_2 }

expr_1: 
    | expr_0 { ("+"|"-") expr_1 }

expr_0:           
    | ["-"] atom { ("*"|"/"|"%") expr_0 }

atom:
    | var
    | lit
    | list
    | "(" expr ")"

var:
    | NAME { "." NAME | "[" expr "]" | "(" [expr_sequence] ")" }

lit:
    | INT
    | FLOAT
    | CHAR
    | BOOL
    | STR

list "[" [expr_sequence] "]"